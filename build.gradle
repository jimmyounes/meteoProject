plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id "io.github.http-builder-ng.http-plugin" version "0.1.1"

}

version = '0.0.0'
sourceCompatibility = 11
targetCompatibility = 11

test {
    useJUnitPlatform()
    minHeapSize = "128m"
    maxHeapSize = "1024m"

    // Headless test execution (true by default)
    if (project.hasProperty("headless") ? project.headless : true) {
        jvmArgs "-Dtestfx.robot=glass"
        jvmArgs "-Dtestfx.headless=true"
        jvmArgs "-Dprism.order=sw"
        jvmArgs "-Dprism.text=t2k"
    }

    beforeTest {
        descriptor -> logger.lifecycle("Running test: " + descriptor)
    }
}

sourceSets.main.resources { srcDirs = ["src/main/java"]; include "**/*.*" }

javafx {
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'tangrammer', name: 'java-net-http-client', version: '0.1.2'


    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    compile "com.github.penemue:keap:0.3.0"

    compile 'org.kordamp.ikonli:ikonli-javafx:11.1.0'
    compile 'org.kordamp.ikonli:ikonli-fontawesome5-pack:11.1.0'
    compile 'org.kordamp.ikonli:ikonli-material-pack:11.1.0'
    compile 'org.codehaus.griffon:griffon-javafx:2.15.1'
    compile 'org.controlsfx:controlsfx:11.0.0-RC2'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.4.0"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.0"

    testCompile "org.testfx:testfx-junit5:4.0.15-alpha"
    testRuntime ("org.testfx:openjfx-monocle:jdk-11+26") {
        exclude group: 'org.openjfx'
    }
}

task(runCLI, dependsOn: 'classes', type: JavaExec) {
    main = 'app.appmeteo.AppMeteoCLI'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    args "list"
    args "today"
}

application {
    mainClassName = 'app.appmeteo.AppMeteo'
    applicationDefaultJvmArgs = [
            '--add-opens=javafx.base/com.sun.javafx.event=ALL-UNNAMED'
    ]
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
}
